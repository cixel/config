# creates a NixOS system configuration
{ self, nixpkgs, inputs }:

name:
{ system
, user
, darwin ? false
,
}:

let
  systemFunc =
    if darwin
    then inputs.darwin.lib.darwinSystem
    else nixpkgs.lib.nixosSystem;
  home-manager =
    if darwin
    then inputs.home-manager.darwinModules
    else inputs.home-manager.nixosModules;

  pkgs = import nixpkgs {
    inherit system overlays;
  };
  overlays = [
    (self: super: {
      go = super.go.overrideAttrs (old: rec {
        version = "1.22.2";
        src = pkgs.fetchurl {
          url = "https://go.dev/dl/go${version}.src.tar.gz";
          hash = "sha256-N06oKyiexzjpaCZ8rFnH1f8YD5SSJQJUeEsgROkN9ak=";
        };
        patches = [ ] ++ (
          if darwin then [ ../home-manager/fd_fsync_darwin.patch ]
          else [ ]
        );
        GOROOT_BOOTSTRAP = "${super.go_1_21}/share/go";
      });
    })
  ];

  machineConfig = ./machines/${name}.nix;
  darwinConfig = import ./darwin.nix { inherit user; };
  nixosConfig = import ./nixos.nix { inherit user; };
  homeConfig = {
    home-manager.useGlobalPkgs = true;
    home-manager.useUserPackages = true;
    home-manager.users.${user} = import ../home-manager/home.nix {
      inherit darwin;
    };
  };

  baseConfig = { pkgs, ... }: {
    # Auto upgrade nix package and the daemon service.
    # services.nix-daemon.enable = true;

    # List packages installed in system profile. To search by name, run:
    # $ nix-env -qaP | grep wget
    # environment.systemPackages = [ ];

    # Set Git commit hash for darwin-version.
    system.configurationRevision = self.rev or self.dirtyRev or null;

    # TODO: there are settings in old /etc/nix/nix.conf generated by
    # the detsys nix installer which i haven't migrated here yet
    nix.settings.experimental-features = ''
      nix-command flakes
    '';

    # The platform the configuration will be used on.
    nixpkgs.hostPlatform = system;
  };
in
systemFunc {
  inherit system;

  modules = [
    { nixpkgs.overlays = overlays; }

    baseConfig
    (if darwin then darwinConfig else nixosConfig)
    machineConfig

    home-manager.home-manager
    homeConfig
  ];
}
